1000 '   PROGRAM NAME     : "DOCTOR"  Ver. 2.1
1020 '   TRANSLATED FOR  :
1030 '   HEATH/ZENITH BY :  Louis E. Frenzel III
1040 '
1050 '  I.    Supervisor
1060 '
1070 CLEAR 1000
1080 GOSUB 1160       ' Initialization
1090 GOSUB 1250       ' Input (and associated functions)
1100 GOSUB 1370       ' Swap word for opposite form
1110 GOSUB 1550       ' Keyword processing
1120 GOSUB 2100       ' Marker removal
1130 GOSUB 2180       ' Doctor's reply
1140 GOTO 1090
1150 '
1160 ' II.     Initialization
1170 '
1180 NM = 28
1190 NU = 82
1200 RANDOMIZE (VAL(RIGHT$(TIME$, 2))*100)
1210 CLS
1220 PRINT "PLEASE STATE YOUR PROBLEM."
1230 RETURN
1240 '
1250 ' III.   Input (and associated functions)
1260 '
1270 RESTORE
1280 X = 0:PA$ = ""
1290 LINE INPUT PA$
1300 IF PA$ = "" THEN PRINT "DO YOU HAVE A PROBLEM?": X = 1:GOTO 1350
1301 PU = ""
1302 FOR U = 1 TO LEN(PA$)
1303 IF ASC(MID$(PA$,U,1)) > 96 AND ASC(MID$(PA,U,1)) < 122 THEN PU$ = PU$ + CHR$(ASC(MID$(PA$,U,1)) - 32) ELSE PU$ = PU$ + MID$(PA$,U,1)
1304 NEXT U
1305 PA$ = PU$
1310 IF PA$ = PR$ THEN PRINT "PLEASE DON'T REPEAT YOURSELF.":X = 1: GOTO 1350
1320 PR$ = PA$
1330 IF RIGHT$(PA$,1) = " " OR RIGHT$(PA$,1) = "!" OR RIGHT$(PA$,1) = "?" THEN L = 1 ELSE L = 0
1340 PA$ = " " + LEFT$(PA$, LEN(PA$) - L) + " "
1350 RETURN
1360 '
1370 '  IV.    Swap word for opposite form
1380 '
1390 IF X = 1 THEN 1530
1400 FOR I = 1 TO NM/2
1410 READ TE$, TN$
1420 FOR PS = 1 TO LEN(PA$) - LEN(TE$) + 1
1430 IF TE$ = MID$(PA$, PS, LEN(TE$)) THEN PA$ = LEFT$(PA$, PS - 1) + TN$ + MID$(PA$, PS + LEN(TE$))
1440 DATA " MOM ", " MOTHER ", " DAD ", " FATHER "
1450 DATA " DREAMS ", " DREAM "
1460 DATA " I ", " YOU@ ", " YOU ", " I ", " ME ", " YOU "
1470 DATA " MY ", " YOUR* "
1480 DATA " YOUR ", " MY ", " MYSELF ", " YOURSELF "
1490 DATA " YOURSELF ", " MYSELF "
1500 DATA " I'M ", " YOU'RE* ", " YOU'RE ", " I'M ", " AM ", " ARE@ "
1510 DATA " WERE ", " WAS "
1520 NEXT PS, I
1530 RETURN
1540 '
1550 ' V.    Key word processing
1560 '
1570 '   A.    Checking for key words
1580 '
1590 IF X = 1 THEN 2080
1600 FOR I = 1 TO NU
1610 READ KE$, JU
1620 FOR PS = 1 TO LEN(PA$) - LEN(KE$) + 1
1630 IF KE$ = MID$(PA$, PS, LEN(KE$)) THEN DR$ = MID$(PA$, PS + LEN(KE$)): IF DR$ <> "" THEN 1660 ELSE 2080
1640 NEXT PS, I
1650 GOTO 1960
1660 DR$ = LEFT(DR$, LEN(DR$) - 1):GOTO 2080
1670 DATA "COMPUTER",1,"MACHINE",1
1680 DATA " NAME",2,"ALIKE",3," LIKE",3," SAME",3
1690 DATA "YOU@ REMEMBER",4,"DO I REMEMBER",5,"YOU@ DREAMED",6
1700 DATA "DREAM",7," IF ",8,"EVERYBODY",9,"EVERYONE",9
1710 DATA "NOBODY",9,"NO ONE",9,"WAS YOU@",10,"YOU@ WAS",10
1715 DATA "L ON YOUR*",42
1720 DATA "WAS I",12,"YOUR* MOTHER",13,"YOUR* FATHER",13
1730 DATA "YOUR* SISTER",13,"YOUR* BROTHER",13,"YOUR* WIFE",13
1740 DATA "YOUR* HUSBAND",13,"YOUR* CHILDREN",13,"YOUR*",14
1750 DATA "ALWAYS",15,"ARE I",16,"ARE@ YOU@",18,"HOW ",25
1760 DATA "BECAUSE",19,"CAN I",20,"CAN YOU@",21,"CERTAINLY",22
1770 DATA "DEUTSCH",23,"ESPANOL",23,"FRANCAIS",23,"HELLO",24
1780 DATA "I REMIND YOU OF",3,"I ARE",26,"I'M",26
1790 DATA "ITALIANO",23,"MAYBE",28,"MY",29," NO ",30
1800 DATA "PERHAPS",28,"SORRY",31,"WHAT ",25,"WHEN ",25
1810 DATA "WHY DON'T I",32,"WHY CAN'T YOU@",33,"YES",22
1820 DATA "YOU@ WANT",34,"YOU@ NEED",34," ARE ",17," I ",27
1830 DATA "YOU@ ARE@ SAD",35,"YOU'RE* SAD",35
1840 DATA "YOU@ ARE@ UNHAPPY",35,"YOU'RE* UNHAPPY",35
1850 DATA "YOU@ ARE@ DEPRESSED",35,"YOU'RE* DEPRESSED",35
1860 DATA "YOU@ ARE@ SICK",35,"YOU'RE* SICK",35
1870 DATA "YOU@ ARE@ HAPPY",36,"YOU'RE* HAPPY",36
1880 DATA "YOU@ ARE@ ELATED",36,"YOU'RE* ELATED",36
1890 DATA "YOU@ ARE@ GLAD",36,"YOU'RE* GLAD",36
1900 DATA "YOU@ ARE@ BETTER",36,"YOU'RE* BETTER",36
1910 DATA "YOU@ FEEL YOU@",37,"YOU@ THINK YOU@",37
1920 DATA "YOU@ BELIEVE YOU@",37,"YOU@ WISH YOU@",37
1930 DATA " YOU@ ARE@",38,"YOU'RE*",38,"YOU@ CAN'T",39
1940 DATA "YOU@ CANNOT",39,"YOU@ DON'T",40,"YOU@ FEEL",41
1950 '
1960 '     B.   No key words found
1970 '
1980 IF YO$ = "" THEN 1990 ELSE RAN = RND * 5 + 1 : ON RAN GOTO 1990, 1990, 1990, 2040, 2040
1990 RAN = RND * 4 + 1 : ON RAN GOTO 2000, 2010, 2020, 2030
2000 PRINT "I AM NOT SURE I UNDERSTAND YOU FULLY.": X = 1:GOTO 2080
2010 PRINT "PLEASE GO ON.": X = 1: GOTO 2080
2020 PRINT "WHAT DOES THAT SUGGEST TO YOU?": X = 1:GOTO 2080
2030 PRINT "DO YOU FEEL STRONGLY ABOUT DISCUSSING SUCH THINGS?": X = 1:GOTO 2080
2040 RAN = RND * 3 + 1 : ON RAN GOTO 2050, 2060, 2070
2050 PRINT "LET'S DISCUSS FURTHER WHY YOUR" + YO$ + ".": X = 1:GOTO 2080
2060 PRINT "EARLIER YOU SAID YOUR" + YO$ + ".": X = 1:GOTO 2080
2070 PRINT "DOES THAT HAVE ANYTHING TO DO WITH THE FACT THAT YOUR" + YO$ + "?": X = 1:GOTO 2080
2080 RETURN
2090 '
2100 ' VI.   Marker remover
2110 '
2120 IF X = 1 THEN 2160
2130 FOR PS = 1 TO LEN(DR$)
2140 IF MID$(DR$, PS, 1) = "@" OR MID$(DR$, PS, 1) = "*" THEN DR$ = LEFT$(DR$, PS - 1) + MID$(DR$, PS + 1)
2150 NEXT PS
2160 RETURN
2170 '
2180 '    VII.    Doctor's reply
2190 '
2200 IF X = 1 THEN 2670
2210 ON JU GOTO 2220,2230,2240,2250,2260,2270,2280,2290,2300,2310,2320,2330,2340,2350,2360,2370,2380,2390,2400,2410,2420,2430,2440,2450,2460,2470,2480,2490,2500,2510,2520,2570,2580,2590,2600,2610,2620,2630,2640,2650,2660
2220 PRINT "DO COMPUTERS WORRY YOU?": GOTO 2670
2230 PRINT "I AM NOT INTERESTED IN NAMES.": GOTO 2670
2240 PRINT "IN WHAT WAY?": GOTO 2670
2250 PRINT "DO YOU OFTEN THINK OF" + DR$ + "?": GOTO 2670
2260 PRINT "DID YOU THINK I WOULD FORGET" + DR$ + "?": GOTO 2670
2265 '
2270 PRINT "REALLY. " + DR$ + "?": GOTO 2670
2280 PRINT "WHAT DOES THAT DREAM SUGGEST TO YOU?": GOTO 2670
2290 PRINT "DO YOU THINK IT'S LIKELY THAT IF " + DR$ + "?": GOTO 2670
2300 PRINT "REALLY, " + KE$ + "?": GOTO 2670
2310 PRINT "WHAT IF YOU WERE " + DR$ + "?": GOTO 2670
2320 PRINT "WERE YOU REALLY?": GOTO 2670
2330 PRINT "WOULD YOU LIKE TO BELIEVE I WAS " + DR$ + "?": GOTO 2670
2340 PRINT "TELL ME MORE ABOUT YOUR FAMILY.": GOTO 2670
2350 PRINT "YOUR " + DR$ + ".": GOTO 2670
2360 PRINT "CAN YOU THINK OF A SPECIFIC EXAMPLE?": GOTO 2670
2370 PRINT "WHY ARE YOU INTERESTED IN WHETHER I AM " + DR$ + " OR NOT?": GOTO 2670
2380 PRINT "DID YOU THINK THEY MIGHT NOT BE " + DR$ + "?": GOTO 2670
2390 PRINT "DO YOU YOU BELIEVE YOU ARE" + DR$ + "?": GOTO 2670
2400 PRINT "WHAT IS THE REAL REASON?": GOTO 2670
2410 PRINT "YOU BELIEVE I CAN " + DR$ + "? DON'T YOU?": GOTO 2670
2420 PRINT "WHETHER OR NOT YOU CAN" + DR$ + " DEPENDS ON YOU MORE THAN ME.": GOTO 2670
2430 PRINT "YOU SEEM QUITE POSITIVE.": GOTO 2670
2440 PRINT "I AM SORRY, I SPEAK ONLY ENGLISH":GOTO 2670
2450 PRINT "HOW DO YOU DO.": GOTO 2670
2460 PRINT "WHY DO YOU ASK?": GOTO 2670
2470 PRINT "WHAT MAKES YOU THINK I AM" + DR$ + "?": GOTO 2670
2480 PRINT "WE WERE DISCUSSING YOU, NOT ME.": GOTO 2670
2490 PRINT "YOU DON'T SEEM QUITE CERTAIN.": GOTO 2670
2500 PRINT "WHY ARE YOU CONCERNED OVER MY" + DR$ + "?": GOTO 2670
2510 PRINT "ARE YOU SAYING 'NO' JUST TO BE NEGATIVE?": GOTO 2670
2520 RAN = RND * 4 + 1: ON RAN GOTO 2530, 2540,2550,2560
2530 PRINT "PLEASE DON'T APOLOGIZE":GOTO 2670
2540 PRINT "APOLOGIES ARE NOT NECESSARY": GOTO 2670
2550 PRINT "WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE?": GOTO 2670
2560 PRINT "YOU NEEDN'T FEEL THAT YOU HAVE TO APOLOGIZE.": GOTO 2670
2570 PRINT "DO YOU BELIEVE THAT I DON'T" + DR$ + "?": GOTO 2670
2580 PRINT "DO YOU THINK YOU SHOULD BE ABLE TO" + DR$ + "?": GOTO 2670
2590 PRINT "WHAT WOULD IT MEAN TO YOU IF YOU GOT" + DR$ + "?": GOTO 2670
2600 GOSUB 2740: GOSUB 2790:PRINT "I AM SORRY TO HEAR YOU ARE" + DR$ + ".": GOTO 2670
2610 GOSUB 2740: GOSUB 2790:PRINT "HOW HAVE I HELPED YOU TO BE" + DR$ + "?": GOTO 2670
2620 PRINT "DO YOU REALLY THINK SO?": GOTO 2670
2630 PRINT "IS IT BECAUSE YOU ARE" + DR$ + " THAT YOU CAME TO ME?": GOTO 2670
2640 PRINT "HOW DO YOU KNOW YOU CAN'T" + DR$ + "?": GOTO 2670
2650 PRINT "DON'T YOU REALLY" + DR$ + "?": GOTO 2670
2660 PRINT "TELL ME MORE ABOUT SUCH FEELINGS": GOTO 2670
2670 RETURN
2680 '
2690 '      Special processing if key word is MY
2700 '
2710 IF LEN(DR$) > 11 THEN YO$ = DR$
2720 RETURN
2730 '
2740 '      Take last word of key phrase if '@' is present
2750 '
2760 IF MID$(KE$, 4, 1) = "@" THEN DR$ = RIGHT$(KE$, LEN(KE$) - 9)
2770 RETURN
2780 '
2790 '        Take last word of key phrase if '*' is present
2800 '
2810 IF MID$(KE$, 7, 1) = "*" THEN DR$ = RIGHT$(KE$, LEN(KE$) - 7)
2820 RETURN
9999 END
